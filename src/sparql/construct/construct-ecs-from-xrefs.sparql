prefix owl: <http://www.w3.org/2002/07/owl#>


CONSTRUCT {
  ?c a owl:Class .
  ?c <http://www.w3.org/2002/07/owl#equivalentClass> ?xcURI 
}
WHERE {
 ?c <http://www.geneontology.org/formats/oboInOwl#hasDbXref> ?x .
 ?a <http://www.w3.org/2002/07/owl#annotatedSource> ?c .
 ?a <http://www.w3.org/2002/07/owl#annotatedProperty> <http://www.geneontology.org/formats/oboInOwl#hasDbXref> .
 ?a <http://www.w3.org/2002/07/owl#annotatedTarget> ?x .
 ?a <http://www.geneontology.org/formats/oboInOwl#source> ?v3 .
 FILTER (strStarts(?v3,"MONDO:equivalent"))
 BIND (
    uri(replace(
        concat("http://purl.obolibrary.org/obo/",
               replace(?x, ":", "_")),
        "http://purl.obolibrary.org/obo/Orphanet_",
        "http://www.orpha.net/ORDO/Orphanet_"))
    AS ?xcURI)
}


